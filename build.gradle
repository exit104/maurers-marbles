/*
 * You may only use this file in accordance with the terms and conditions
 * outlined in the accompanying LICENSE file.
 */

plugins {
  id 'checkstyle'
  id "com.github.spotbugs" version "4.5.1"
  id 'jacoco'
  id 'java-library'
  id 'maven-publish'
  id 'pmd'
}

[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

group = 'com.exit104.maurersmarbles'
project.version = '0.0.1'
jar {
  manifest {
    attributes 'Implementation-Version': project.version
  }
}

repositories {
  mavenLocal()
  mavenCentral()
}

checkstyle {
  configFile = rootProject.file('config/checkstyle/checkstyle.xml')
  toolVersion "7.6.1"
}

java {
  withJavadocJar()
  withSourcesJar()
}

pmd {
  ruleSets = []
  ruleSetFiles "config/pmd/pmd.xml"
}

jacocoTestReport {
  reports {
    xml.enabled true
    html.enabled true
  }
}

spotbugsMain {
  reports {
    xml.enabled = false
    html.enabled = true
  }
}

spotbugsTest {
  reports {
    xml.enabled = false
    html.enabled = true
  }
}

gradle.projectsEvaluated {
  tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
  }
}

dependencies {
  implementation group: 'com.google.guava', name: 'guava', version: '26.0-android'
  testImplementation group: 'junit', name: 'junit', version: '4.10'
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifactId 'maurers-marbles'
      from components.java
    }
  }
}
